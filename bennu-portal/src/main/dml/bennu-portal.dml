package org.fenixedu.bennu.portal.domain;

public class PortalConfiguration {
	public LocalizedString applicationTitle (REQUIRED);
	public LocalizedString htmlTitle (REQUIRED);
	public LocalizedString applicationSubTitle (REQUIRED);
	public LocalizedString applicationCopyright (REQUIRED);
	public String theme (REQUIRED);
	public String supportEmailAddress;
	public String systemEmailAddress;
	public bytearray logo;
	public String logoType;
}

relation BennuPortalConfiguration {
    protected .org.fenixedu.bennu.core.domain.Bennu playsRole root {
        multiplicity 1..1;
    }
    public PortalConfiguration playsRole configuration {
        multiplicity 0..1;
    }
}

public class MenuItem {
	protected Integer ord (REQUIRED);
	protected LocalizedString title (REQUIRED);
	protected LocalizedString description (REQUIRED);
	protected String path (REQUIRED);
	protected String fullPath (REQUIRED);
	protected Boolean visible (REQUIRED);
	public String layout;
}

public class MenuContainer extends MenuItem {
}

public class MenuFunctionality extends MenuItem {
    protected String itemKey;
    protected String provider;
}

relation ApplicationMenu {
    protected PortalConfiguration playsRole configuration {
        multiplicity 0..1;
    }
    protected MenuContainer playsRole menu {
        multiplicity 1..1;
    }
}

relation MenuItemComposition {
	protected MenuContainer playsRole parent {
        multiplicity 0..1;
	}
	public MenuItem playsRole child {
		multiplicity *;
	}
}

relation MenuItemGroup {
    protected MenuItem playsRole menuItem {
        multiplicity *;
    }
    protected .org.fenixedu.bennu.core.domain.groups.Group playsRole accessGroup {
        multiplicity 1..1;
    }
}